// Most of the code was (with slight modifications) copied from http://homepages.warwick.ac.uk/staff/S.Siksek/progs/cubicmodularity/Xb5b7.m

Qx<x>:=PolynomialRing(Rationals());

X:=SmallModularCurve(71);

cusps:=&cat[CuspPlaces(X,71,d) : d in Divisors(71)];

assert &and[Degree(P) eq 1 : P in cusps];
assert #NonCuspidalQRationalPoints(X,71) eq 0;

Y:=HyperellipticCurve(x^13-x^12-10*x^11-20*x^10-7*x^9+27*x^8+36*x^7-31*x^5-18*x^4+7*x^3+10*x^2+x-3, -x^7-x^5-x^4-x^3-1);

tf,phi:=IsIsomorphic(X,Y);
assert tf;

cusps:={Pushforward(phi,c) : c in cusps};

ptsY:= [ Y![1 , 1 , 0], Y![1 , 0 , 0]];
assert cusps eq {Place(p) : p in ptsY};

p0:=ptsY[1];

J:=Jacobian(Y);
ptsJ:=[ p-p0 : p in ptsY   ];
assert &and[35*Q eq J!0 : Q in ptsJ];
assert BadPrimes(J) eq [71];

J3:=BaseChange(J,GF(3));
B,mu:=AbelianGroup(J3);

A:=FreeAbelianGroup(2);
eps:=hom<A->B | [ (J3!Q)@@mu : Q in ptsJ  ] >;
C:=Image(eps);

Q1:=ptsJ[2];
mu1:=(J3!Q1)@@mu;

cuspGp:=AbelianGroup([35]);
delta:=hom<cuspGp->C | [mu1]>;
assert #Kernel(delta) eq 1;
assert Image(delta) eq C;

cuspGpElts:={a*Q1 : a in [0..34]};
assert #cuspGpElts eq 35;

D1:=Place(ptsY[2])-Place(ptsY[1]);

infdiv:=3*Place(ptsY[1]);

deg3:=[a*D1+infdiv : a in [0..34]];

assert #deg3 eq 35;
assert &and[Degree(D) eq 3 : D in deg3];

assert &and[Dimension(RiemannRochSpace(D)) in [0,1,2] : D in deg3];

deg3:=[ D : D in deg3 | Dimension(RiemannRochSpace(D)) eq 1];

assert #deg3 eq 2;

deg3new:=[];
for D in deg3 do
L,mu:=RiemannRochSpace(D);
assert Dimension(L) eq 1;
D1:=D+Divisor(mu(L.1));
assert IsEffective(D1);
decomp:=Decomposition(D1);
if #decomp eq 1 and decomp[1,2] eq 1 then
Append(~deg3new,decomp[1,1]);
end if;
end for;

assert #deg3new eq 0;
// thus there are no irreducible divisors of degree 3

// now we find all points of degree 4

D1:=Place(ptsY[2])-Place(ptsY[1]);

infdiv:=4*Place(ptsY[1]);

deg4:=[a*D1+infdiv : a in [0..34]];

assert #deg4 eq 35;
assert &and[Degree(D) eq 4 : D in deg4];

assert &and[Dimension(RiemannRochSpace(D)) in [0,1,2,3] : D in deg4];

deg4:=[ D : D in deg4 | Dimension(RiemannRochSpace(D)) eq 1];

assert #deg4 eq 2;

deg4new:=[];
for D in deg4 do
L,mu:=RiemannRochSpace(D);
assert Dimension(L) eq 1;
D1:=D+Divisor(mu(L.1));
assert IsEffective(D1);
decomp:=Decomposition(D1);
if #decomp eq 1 and decomp[1,2] eq 1 then
Append(~deg4new,decomp[1,1]);
end if;
end for;

assert #deg4new eq 0;
// thus there are no irreducible divisors of degree 4

// now we find all points of degree 5

D1:=Place(ptsY[2])-Place(ptsY[1]);

infdiv:=5*Place(ptsY[1]);

deg5:=[a*D1+infdiv : a in [0..34]];

assert #deg5 eq 35;
assert &and[Degree(D) eq 5 : D in deg5];

assert &and[Dimension(RiemannRochSpace(D)) in [0,1,2,3] : D in deg5];

deg5:=[ D : D in deg5 | Dimension(RiemannRochSpace(D)) eq 1];

assert #deg5 eq 2;

deg5new:=[];
for D in deg5 do
L,mu:=RiemannRochSpace(D);
assert Dimension(L) eq 1;
D1:=D+Divisor(mu(L.1));
assert IsEffective(D1);
decomp:=Decomposition(D1);
if #decomp eq 1 and decomp[1,2] eq 1 then
Append(~deg5new,decomp[1,1]);
end if;
end for;

assert #deg5new eq 0;
// thus there are no irreducible divisors of degree 5

// now we find all points of degree 6

D1:=Place(ptsY[2])-Place(ptsY[1]);

infdiv:=6*Place(ptsY[1]);

deg6:=[a*D1+infdiv : a in [0..34]];

assert #deg6 eq 35;
assert &and[Degree(D) eq 6 : D in deg6];

assert &and[Dimension(RiemannRochSpace(D)) in [0,1,2,3,4] : D in deg6];

deg6:=[ D : D in deg6 | Dimension(RiemannRochSpace(D)) eq 1];

assert #deg6 eq 30;

deg6new:=[];
for D in deg6 do
L,mu:=RiemannRochSpace(D);
assert Dimension(L) eq 1;
D1:=D+Divisor(mu(L.1));
assert IsEffective(D1);
decomp:=Decomposition(D1);
if #decomp eq 1 and decomp[1,2] eq 1 then
Append(~deg6new,decomp[1,1]);
end if;
end for;

assert #deg6new eq 28;
// thus there are 28 irreducible divisors of degree 6
